# How to inherit parent terragrunt.hcl inputs block

**yagehu** commented *Jan 20, 2020*

In `prod/terragrunt.hcl` there's an inputs block like so:

```
# Configure root level variables that all resources can inherit. This is especially helpful with multi-account configs
# where terraform_remote_state data sources are placed directly into the modules.
inputs = merge(
  # Configure Terragrunt to use common vars encoded as yaml to help you keep often-repeated variables (e.g., account ID)
  # DRY. We use yamldecode to merge the maps into the inputs, as opposed to using varfiles due to a restriction in
  # Terraform >=0.12 that all vars must be defined as variable blocks in modules. Terragrunt inputs are not affected by
  # this restriction.
  yamldecode(
    file("${get_terragrunt_dir()}/${find_in_parent_folders("region.yaml", local.default_yaml_path)}"),
  ),
  yamldecode(
    file("${get_terragrunt_dir()}/${find_in_parent_folders("env.yaml", local.default_yaml_path)}"),
  ),
  {
    aws_profile                  = "prod"
  },
)
```

How can children terragrunt.hcl files reference the variables such as `env` and `region`?
<br />
***


**sdot257** commented *Feb 21, 2020*

I was spinning in circles with this issue and here I am. Is this possible? 

In my `terragrunt.hcl` I want to be able to reference the a variable called `env_id` from the `env` file. It would be ideal if I set the `env_id` in one file and share it across all child terragrunt.hcl file.
***

**yorinasub17** commented *Feb 21, 2020*

This is now possible with `read_terragrunt_config` released this week in https://github.com/gruntwork-io/terragrunt/releases/tag/v0.22.3. We just haven't had the chance to update the examples yet.
***

**sdot257** commented *Feb 22, 2020*

How would I use this as follows? Here's the error message I got.

` Unsupported attribute; This object does not have an attribute named "environment".`

# Code
```
{~/tf-infrastructure/hosting/us-east-1/dev/sg-internal-all}
-> cat ../common.hcl
inputs = {
  environment = "dev"
}
```

```
~/tf-infrastructure/hosting/us-east-1/dev/sg-internal-all/terragrunt.hcl

locals {
  common_vars = read_terragrunt_config(find_in_parent_folders("common.hcl"))
}

inputs = merge(
  local.common_vars.inputs,
  {
    name = "${local.common_vars.environment}-internal-all"
  }
)
```
***

**yorinasub17** commented *Feb 22, 2020*

`local.common_vars.environment` should be `local.common_vars.inputs.environment`. See [the example in the docs](https://terragrunt.gruntwork.io/docs/reference/built-in-functions/#read_terragrunt_config), as well as [our test config](https://github.com/gruntwork-io/terragrunt/blob/master/test/fixture-read-config/full/terragrunt.hcl).
***

**sdot257** commented *Feb 22, 2020*

THanks!!!

You know, I thought i tried that but it is working now. Question, what does this line do? What does it give me if I leave it in my code? My code seems to work without it as well.



```
inputs = merge(
  local.common_vars.inputs,
```
***

**yorinasub17** commented *Feb 22, 2020*

This merges the two maps together. You can use this to add to, or update the inputs from the common vars. For example, if your common vars was:

```hcl
inputs = {
  env = "dev"
}
```

and you had:

```hcl
inputs = merge(
  local.common_vars.inputs,
  { region = "us-east-1" },
)
```

the result is:

```hcl
{
  env = "dev"  # comes from common vars
  region = "us-east-1"  # comes from the map being merged
}
```

Or if you had:

```hcl
inputs = merge(
  local.common_vars.inputs,
  { env = "prod" },
)
```

the result is:

```hcl
{
  env = "prod"  # the second map overrode the value of the first map
}
```
***

**speterson-zoll** commented *Mar 19, 2020*

Grabbing onto this thread, I'm still not understanding something about the inner workings of variable passing in Terragrunt.

In the example code, in the terragrunt.hcl file under non-prod, I see:

```
inputs = merge(
  local.account_vars.locals,
  local.region_vars.locals,
  local.environment_vars.locals,
)
```

with an explanation that leads me to believe that I can now access these variables elsewhere in the Live repo.  If I add a new module folder, say vpc, at the same level as mysql, and add a terragrunt.hcl file in that folder, how do I access the ```aws_region``` variable from the higher level terragrunt.hcl file, or, at the very least, how do I get the region information?  It doesn't seems like I should have to add a whole new ```read_terragrunt_config``` in my new terragrunt.hcl file.  I'm unclear about how to get what's described as GLOBAL PARAMETERS in the comments.  Any examples I've looked at refer to locals in the same file, not inputs in a different file.

Any help is appreciated!
***

**yorinasub17** commented *Mar 19, 2020*

> with an explanation that leads me to believe that I can now access these variables elsewhere in the Live repo

You can't interpolate `inputs` within terragrunt configurations. Only `locals`. However, `locals` do not merge in terragrunt so there is no way to inherit common variables through `include`.

Global parameters is something we currently don't support in terragrunt, and the only mechanism that exists right now is `read_terragrunt_config`. There is a history to this that I don't want to get into now, but the tl;dr is that we will be working on a new block called `import` that will be a better version of `include` that handles this use case. You can read more about it in [the RFC](https://github.com/gruntwork-io/terragrunt/blob/master/_docs/rfc/imports.md).
***

**speterson-zoll** commented *Mar 19, 2020*

Ah, OK, thanks.  All my experimenting was running up in the face of the Global Parameter comment, which is misleading.
***

**fdamstra** commented *Jun 16, 2020*

I'm still struggling. What does this code in the example accomplish, then?

From https://github.com/gruntwork-io/terragrunt-infrastructure-live-example/blob/master/terragrunt.hcl
```
# ---------------------------------------------------------------------------------------------------------------------
# GLOBAL PARAMETERS
# These variables apply to all configurations in this subfolder. These are automatically merged into the child
# `terragrunt.hcl` config via the include block.
# ---------------------------------------------------------------------------------------------------------------------

# Configure root level variables that all resources can inherit. This is especially helpful with multi-account configs
# where terraform_remote_state data sources are placed directly into the modules.
inputs = merge(
  local.account_vars.locals,
  local.region_vars.locals,
  local.environment_vars.locals,
)
```

The accompanying readme attempts to explain it, but it's doesn't help me at all.

```
## Creating and using root (account) level variables

In the situation where you have multiple AWS accounts or regions, you often have to pass common variables down to each
of your modules. Rather than copy/pasting the same variables into each `terragrunt.hcl` file, in every region, and in
every environment, you can inherit them from the `inputs` defined in the root `terragrunt.hcl` file.
```

Are these being passed into the modules? If so, how do the modules access them?


***

**brikis98** commented *Jun 17, 2020*

The example you're referring to has a _root_ `terragrunt.hcl` plus a bunch of _child_ `terragrunt.hcl` that use [`include`](https://terragrunt.gruntwork.io/docs/reference/config-blocks-and-attributes/#include) to include all the settings from the root. That means all the child modules include the [`inputs`](https://terragrunt.gruntwork.io/docs/reference/config-blocks-and-attributes/#inputs) from that root `terragrunt.hcl`. Those child modules can also specify additional `inputs`, and Terragrunt will merge them all together and pass them to the Terraform module via env vars. This is useful, for example, if every Terraform module you has requires a few common parameters to be passed in (e.g., `aws_region`, `aws_account_id`, etc), as you can define those variables once in the root `terragrunt.hcl` rather than having to copy/paste them into every single child.
***

**fdamstra** commented *Jun 17, 2020*

Thank you. So, if I understand correctly, the `locals` defined in the root `terragrunt.hcl` file, though this mechanism of translating/merging them into an `inputs` section, can be available to `terraform-modules` by specifying them as variables (probably in their respective `vars.tf`), and then referencing them as normal (e.g. `var.account_name`).

***

**brikis98** commented *Jun 18, 2020*

Yes. Any variable `xxx` you set in `inputs` gets passed to your Terraform module via an environment variable of the name `TF_VAR_xxx`. If the module has a `variable "xxx"`, you can then read that value using `var.xxx`. You can set the values of `inputs` to constants (e.g., `xxx = "foo"`) or to local variables (e.g., `xxx = local.foo`). The `inputs` also get merged with `inputs` defined in the root `terragrunt.hcl`.
***

**NIVJAIN** commented *Dec 23, 2020*

# stage/prod/terragrunt.hcl 
  terragrunt child file inside prod
`include {
  path = find_in_parent_folders()
}
locals {
  aws_region = "ap-southeast-1"
  account_name   = "bitcoin"
}
terraform {
  source = "../../modules/vpc/"
  extra_arguments "common_vars" {
    #commands = ["plan", "apply"]
     commands = get_terraform_commands_that_need_vars()

 arguments = [
      "-var-file=production.tfvars"
    ]
  }
}`

# stage/terragrunt.hcl
`locals {
  account_vars = read_terragrunt_config(find_in_parent_folders("account.hcl"))
    // account_vars = read_terragrunt_config(find_in_parent_folders("terragrunt.hcl"))
  account_name = local.account_vars.locals.account_name
}

generate "provider" {
  path = "provider.tf"  //removed content here for simplicity
}
remote_state {
  backend = "s3"
  generate = {
    path      = "backend.tf"
    if_exists = "overwrite"
  }
  config = {
    bucket         = "terragrunt-state-jain"
    key            = "${path_relative_to_include()}/${local.account_name}-terraform.tfstate"
    region         = "ap-southeast-1"
    encrypt        = true
    dynamodb_table = "my-lock-table"
  }
}
inputs = merge(
  local.account_vars.locals,
)`

My question is it possible to pass the variables directly from child terragrunt.hcl file to parent terragrunt.hcl file, as i need to create dynamic state file for "key attribute"   key  = "${path_relative_to_include()}/${local.account_name}-terraform.tfstate".

i
***

**brikis98** commented *Jan 6, 2021*

Please use [fenced code blocks](https://docs.github.com/en/free-pro-team@latest/github/writing-on-github/creating-and-highlighting-code-blocks), as otherwise, your code is very hard to read. I can then take a look at your question.
***

**bitsofinfo** commented *Apr 5, 2021*

starting to use terragrunt and running into these same issues. I'm trying to follow the the keep your configuration DRY examples (i.e. live vs module repos) and trying to leverage a hierarchy of `terragrunt.hcl` files to reduce the amount of inputs i need to declare... but I'm not seeing inputs merged and I'm having to re-declare inputs all over the place defeating the purpose of this DRY principle. I feel like i'm just trading terraform code repetitions for terragrunt code repetitions.
***

**invsblduck** commented *Sep 19, 2021*

Here's a fixed link to the [Imports RFC](https://github.com/gruntwork-io/terragrunt/blob/master/docs/_docs/05_rfc/imports.md).
***

**yorinasub17** commented *Sep 28, 2021*

Just merged an update to this repo that makes use of the [recently added multiple includes feature](https://github.com/gruntwork-io/terragrunt/releases/tag/v0.32.0).

In particular, for this specific issue, you can reference the root `inputs` by taking advantage of exposed includes:

```
include "root" {
  path = find_in_parent_folders()
  expose = true
}

inputs = {
  env_name = include.root.inputs.env
}
```

Note that there are limitations to where you can access certain exposed included elements, which you should be aware of. This is documented in [the include docs](https://terragrunt.gruntwork.io/docs/reference/config-blocks-and-attributes/#include), specifically section "Limitations on accessing exposed config".

Closing this as the original question has been answered. If you have terragrunt feature requests to improve DRY architecture further, please open an issue in the `terragrunt` repo. Thanks!
***

**justinTM** commented *Oct 19, 2021*

> I feel like i'm just trading terraform code repetitions for terragrunt code repetitions.

because you are lol! bug in terragrunt. that feature above does not answer the question, it is instead a hacky workaround. terragrunt should, without any extra effort on the part of the user, merge all parent values with child values, including `locals` and `inputs` blocks. anything else is unacceptable as the entire purpose of terragrunt is to solve *precisely* this problem.
***

**archenroot** commented *Nov 2, 2021*

@justinTM - I think its valid implementation as soon as if you follow the https://github.com/gruntwork-io/terragrunt-infrastructure-live-example with top,account,region,env layer where top hcl merges all 3 then the way presented here you get them just via anyway existing include
***

